/*
 *  Copyright (c) 2020 Raffael Herzog
 *
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to
 *  deal in the Software without restriction, including without limitation the
 *  rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 *  sell copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 *  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 *  IN THE SOFTWARE.
 */

configurations {
  processorTestsClasspath
  processorTestsProcessorPath
}
configurations.processorTestsProcessorPath.extendsFrom configurations.testCompile

dependencySets {
  javapoet '1.12.1', 'com.squareup', 'javapoet'
}

dependencies {
  api project(':tools:model')
  api dependencySets.javapoet

  testImplementation dependencySets.tsconfig
  processorTestsClasspath project(':api')
  processorTestsClasspath dependencySets.tsconfig
  processorTestsClasspath sourceSets.test.output.classesDirs
}

def processorMetaDir = project.file('src/main/processor')

processResources {
  from processorMetaDir
}


task prepareProcessorTests() {
  dependsOn testClasses
  doFirst {
    def processorTestsProperties = file("$buildDir/processor-tests.properties")
    mkdir processorTestsProperties.parent
    Properties props = new Properties()
    props.setProperty('classpath', configurations.processorTestsClasspath.asPath)
    props.setProperty('processor-path', configurations.processorTestsProcessorPath.asPath)
    processorTestsProperties.withOutputStream {props.store(it, "Generated by gradle ${new Date()}")}
  }
}
test.dependsOn prepareProcessorTests

idea.module {
  testSourceDirs += file('src/test/cases')
}
